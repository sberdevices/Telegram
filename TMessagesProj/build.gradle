buildscript {
    apply from: "$project.projectDir/internal_helpers.gradle"

    if (isInternalProjectBuild() && rootProject.findProject(':plugins:star-app') != null) {
        dependencies {
            classpath 'ru.sberdevices.starplugin:star-app'
        }
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

if (isInternalProjectBuild()) {
    apply plugin: 'ru.sberdevices.starapp'
}

configurations {
    compile.exclude module: 'support-v4'
}

configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'androidx.recyclerview', module: 'recyclerview'
    exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    ndkVersion "21.1.6352462"

    defaultConfig.applicationId = "ru.sberdevices.telegramcalls"

    sourceSets.main.jniLibs.srcDirs = ['./jni/']

    externalNativeBuild {
        cmake {
            version '3.18.1'
            path 'jni/CMakeLists.txt'
        }
    }

    lintOptions {
        abortOnError false
        baseline file("config/lint-baseline.xml")
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        disable 'BlockedPrivateApi'
    }

    dexOptions {
        jumboMode = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude("META-INF/*.kotlin_module")
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ""
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.debug {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
    }

    sourceSets.release {
        manifest.srcFile 'config/release/AndroidManifest.xml'
    }

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 29
        versionName "1.02"
        versionCode = 2206 * 10 + 1

        vectorDrawables.generatedDensities = ['mdpi']

        ndk {
            abiFilters "armeabi-v7a"
        }

        externalNativeBuild {
            cmake {
                version '3.18.1'
                arguments '-DANDROID_STL=c++_static', "-j=16", "-DCMAKE_BUILD_TYPE=Release"
            }
        }
    }
}

dependencies {
    if (isInternalProjectBuild()) {
        // for internal build, internal deps are placed into libs inside build folder
        implementation fileTree(include: ['*.aar'], dir: "$project.buildDir/input_aar_libs")
    } else {
        // for external build, internal deps are placed into standard libs folder
        implementation fileTree(include: ['*.aar'], dir: "libs")
    }

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.sharetarget:sharetarget:1.0.0"

    implementation "com.squareup.picasso:picasso:2.71828"

    compileOnly 'org.checkerframework:checker-qual:2.5.2'
    compileOnly 'org.checkerframework:checker-compat-qual:2.5.0'
    implementation 'com.google.firebase:firebase-messaging:21.0.1'
    implementation 'com.google.firebase:firebase-config:20.0.2'
    implementation 'com.google.firebase:firebase-datatransport:17.0.10'
    implementation 'com.google.firebase:firebase-appindexing:19.1.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-vision:16.2.0'
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.1.0'
    implementation 'com.google.android.gms:play-services-wallet:18.1.2'
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6'
    implementation 'com.stripe:stripe-android:2.0.2'
    implementation files('libs/libgsaverification-client.aar')

    // SberDevices
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.20"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.0"

    internal {
        implementation project(':common:smartfocus')
        implementation project(':common:text-processing')
        implementation project(':playsync:sdk')
        implementation project(':public:cv:impl')
        implementation project(':public:services:appstate:impl')
        implementation project(':services-sdk:analytics')
        implementation project(':services-sdk:appconfig')
        implementation project(':services-sdk:calls')
        implementation project(':services-sdk:echocancel')
        implementation project(':services-sdk:settings')
    }

    external {
        implementation "ru.sberdevices.smartapp.sdk.cv:impl:3.0.0"
        implementation "ru.sberdevices.smartapp.sdk.appstate:impl:1.0-rc1"
        implementation "ru.sberdevices.smartapp.sdk.messaging:impl:1.0-rc1"

        implementation files('libs/not-yet-on-maven-deps.aar')
    }
}

apply plugin: 'com.google.gms.google-services'
